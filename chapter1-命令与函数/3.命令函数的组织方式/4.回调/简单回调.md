# 简单回调

回调把命令作为数据进行传递。

把命令数据化，通常有两种思路：命令编号、命令字符串。

命令编号：编号是一个记分板分数，把这个分数作为数据进行传递。调用时，利用一个记分板树索引到对应的命令执行。

命令字符串：用nbt表示命令的字符串，把这个字符串作为数据进行传递。调用时，在命令方块空间里分配一个命令方块，把它的Command标签设置成这个字符串，激活命令方块。

这里提到的<命令方块空间>，我们将在章节<数据结构>中实现它。

两种做法各有优缺点：

记分板树回调具有即时性，可以在函数的上下文中立即完成回调；但记分板树的动态性有所欠缺(毕竟要依靠大量穷举)。

命令方块回调即时性不足，函数调用命令方块只能在本刻的命令方块刻执行，而不是在函数的上下文中立即完成。而且命令方块刻中继续调用一个命令方块的话，只能等到下一刻的命令方块刻执行。如果要连续执行，只能在本函数刻一次性分发所有的命令方块。但命令方块有无限可能的字符串，不需要依赖穷举查询。1.19.4更新字符串切割后，利用命令方块可以实现动态命令，这使命令方块的动态性变得非常强大。

因此，对于回调任务，我们要考察它对即时性的要求。如果对即时性要求低，那么可以使用命令方块回调；如果对即时性要求高，那么只能使用记分板树回调。

动态命令是一个复杂的回调系统，我们将在章节<系统架构设计与优化>把动态命令作为一个系统设计的实例进行讲解。

本章我们只介绍两种简单的命令方块回调设计：

\#例1：实现一个按钮组件，外部开发者可以使用回调命令自定义按钮触发效果。

```mcfunction
#button:triggered
#当按钮被触发时调用此函数
#使用命令方块空间分配1个命令方块，返回到世界实体的位置
scoreboard players set inp int 1
execute as @e[tag=math_marker,limit=1] run function cbspace:_new
execute at @e[tag=math_marker,limit=1] run data modify block ~ ~ ~ Command set from entity @s data.trigger_command
```

\#例2：更方便的计时系统。

利用记分板计时有不方便的地方。

一个简单的例子可以说明它的局限性：一个时间线上，如果在n个时刻发生不同的事件，那么我们需要在循环函数中穷举n条execute if score。

这还仅仅是线性的时间结构，如果出现树形，甚至环形等结构，完全用记分板计时会让逻辑变成一团乱麻。

如果读者制作过时间结构复杂的技能效果，应该有被计时逻辑困扰的经历吧。

schedule指令是一种更加方便的计时方式：我们仅仅是"安排"某件事在多久之后做，而不需要在循环函数中管理它的计时逻辑。但是schedule无法传递执行者等数据，导致它的应用非常受限。

我们现在要实现的，便是使用schedule的计时方式，同时能够传递执行者数据的一个计时系统。

内部构造：

```mcfunction
#timer:tick
execute as @e[tag=time_marker] if score @s timer matches 1.. run function #timer:countdown

#timer:countdown
scoreboard players remove @s timer 1
execute if score @s timer matches ..0 run function timer:execute

#timer:execute
#使用命令方块空间分配2个命令方块
scoreboard players set inp int 2
function cbspace:_new
#抓取执行者的命令方块
execute at @s run data modify block ~ ~ ~ Command set value "execute as @e[tag=time_marker,limit=1,sort=nearest] run function timer:get_self"
#迭代到下一个命令方块
execute at @s run function timer:execute_next

#timer:execute_next
#请注意，两个命令方块是依次执行的，无间隙
#因此第一个命令方块中的临时数据在第二个命令方块中依然生效
function cbspace:_next
execute at @s run data modify block ~ ~ ~ Command set from entity @s data.trigger_command
#位置回溯(记得调用这个函数时的at @s吗)
tp @s ~ ~ ~

#timer:get_self
#使用tag=timer_self表示执行者实体
tag @e remove timer_self
#执行者是距离命令方块中心最近的实体，即是调用该命令方块的time_marker
scoreboard players operation temp0 int = @s timer_uuid0
scoreboard players operation temp1 int = @s timer_uuid1
scoreboard players operation temp2 int = @s timer_uuid2
scoreboard players operation temp3 int = @s timer_uuid3
#请注意这里的uuid匹配操作
execute as @e[tag=timer_uuid] if score @s timer_uuid0 = temp0 int if score @s timer_uuid1 = temp1 int if score @s timer_uuid2 = temp2 int if score @s timer_uuid3 = temp3 int run tag @s add timer_self
#清除计时任务
kill @s
```

外部接口：

```mcfunction
#timer:_init
#外部开发者需要手动执行初始化函数
scoreboard objectives add timer dummy
scoreboard objectives add timer_uuid0 dummy
scoreboard objectives add timer_uuid1 dummy
scoreboard objectives add timer_uuid2 dummy
scoreboard objectives add timer_uuid3 dummy

#timer:_new
#使用数据模板实例化一个计时任务
tag @e remove result
summon marker 0 11 0 {Tags:["result","time_marker"]}
execute as @e[tag=result,limit=1] run function #timer:set

#timer:set
#设置各项属性
execute store result score @s timer run data get storage math:io input.time
execute store result score @s timer_uuid0 run data get storage math:io input.uuid[0]
execute store result score @s timer_uuid1 run data get storage math:io input.uuid[1]
execute store result score @s timer_uuid2 run data get storage math:io input.uuid[2]
execute store result score @s timer_uuid3 run data get storage math:io input.uuid[3]
data modify entity @s data.trigger_command set from storage math:io input.trigger_command

#timer:_setas
#输入一个执行者
#设置tag=timer_self的指代实体
data modify storage math:io input.uuid set from entity @s UUID
#懒加载UUID
execute unless entity @s[tag=timer_uuid] run function timer:set_uuid

#timer:set_uuid
tag @s add timer_uuid
execute store result score @s timer_uuid0 run data get entity @s UUID[0]
execute store result score @s timer_uuid1 run data get entity @s UUID[1]
execute store result score @s timer_uuid2 run data get entity @s UUID[2]
execute store result score @s timer_uuid3 run data get entity @s UUID[3]
```

使用示例：让A实体在5秒后收到。

```mcfunction
#timer:test
#构造数据模板
data modify storage math:io input set value {time:100,trigger_command:"execute as @e[tag=timer_self] run say roger！"}
#指定timer_self的继承者
execute as @e[tag=A,limit=1] run function timer:_setas
#实例化计时任务
function timer:_new
```

本章完。