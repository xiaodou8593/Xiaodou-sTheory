# 断点测试

像执果索因一样，断点测试也是建立在测试函数的可重复性之上的（可见可重复性有多么重要）。

与执果索因不同的是，断点测试不需要知道“显而易见”的问题源头，它只需要得到输入和异常输出，便可以机械地搜索（如果把执果索因比作空降兵精准的定点打击，那么断点测试便是撒一张大网，然后不断收缩，直到找到bug），二者的搜索方向是相反的。

在断点测试中，我们利用二分法决定网的收缩决策。

断点测试的思路是这样的：

1.保证函数的开头和结尾有信息输出（显然首部的信息是正常的，尾部的信息是异常的）。

2.大致找到函数的中间位置，写上tellraw进行信息输出，重复运行一次测试函数，观察信息是否异常。

3.若信息异常，说明至少前半段有bug，对前半段重复进行断点测试。

4.若信息正常，说明bug出现在后半段，对后半段重复进行断点测试。

5.当异常被截取到单条命令时，如果它是一个function命令，我们还可以“进入”到这个mcfunction内部，重复进行断点测试。

6.直到最后搜索到异常的源头。

断点测试实战：

\#例1：中断的循环

```mcfunction
#math:graph/_randnew
data modify storage math:io input set value {nodes:[],pos:[0.0d,0.0d,0.0d],rot:[0.0f,0.0f],uvw_theta:0.0f}
data modify storage math:io temp set value {name:"",to:[],display:{hide_name:0b,item:{},uvw:[0.0d,0.0d,0.0d]}}

#随机生成节点数量
scoreboard players set min int 8
scoreboard players set max int 18
function math:_random
scoreboard players operation tempn int = random int

#洗牌准备
data modify storage math:io list set value []
scoreboard players set loop int 0
execute if score loop int < tempn int run function math:graph/randnew/pre_loop
function math:list/shuffle/_start

#进入循环
setblock 0 11 0 oak_sign
scoreboard players set inp int 3
scoreboard players operation loop int = tempn int
execute if score loop int matches 1.. run function math:graph/randnew/loop

#洗牌结束
function math:list/shuffle/_end

function math:graph/_ssnew
```

```mcfunction
#math:graph/randnew/loop
data modify storage math:io temp.to set value []
#名字
function math:randstring/_pull
data modify block 0 11 0 Text1 set value '{"nbt":"result","storage":"math:io","interpret":true}'
data modify storage math:io temp.name set from block 0 11 0 Text1

#连接
function math:list/shuffle/_next
scoreboard players set min int 0
scoreboard players operation max int = tempn int
function math:_random
scoreboard players operation sloop int = random int
execute if score sloop int matches 1.. run function math:graph/randnew/sloop

#物品
scoreboard players set min int 0
scoreboard players set max int 2
function math:_random
execute if score random int matches 0 run data modify storage math:io temp.display.item set from storage math:class items[0]
execute if score random int matches 1 run data modify storage math:io temp.display.item set from storage math:class items[1]
execute if score random int matches 2 run data modify storage math:io temp.display.item set from storage math:class items[2]

#坐标
scoreboard players set min int -50000
scoreboard players set max int 50000
function math:_random
execute store result storage math:io temp.display.uvw[0] double 0.0001 run scoreboard players get random int
function math:_random
execute store result storage math:io temp.display.uvw[1] double 0.0001 run scoreboard players get random int
function math:_random
execute store result storage math:io temp.display.uvw[2] double 0.0001 run scoreboard players get random int

data modify storage math:io input.nodes append from storage math:io temp

#循环迭代
scoreboard players remove loop int 1
execute if score loop int matches 1.. run function math:graph/randnew/loop
```

以上为涉及到异常的指令。

在_randnew函数中，我们使用随机数生成了节点数量，并进入了数量对应次数的循环。

但是实际运行发现，loop函数仅运行了一次。于是我们很容易发现异常数据是循环变量<loop,int>。

我们使用断点法二分搜索异常。

```mcfunction
#math:graph/randnew/loop
data modify storage math:io temp.to set value []
tellraw @a {"score":{"name":"loop","objective":"int"}}
#省略...
execute if score random int matches 2 run data modify storage math:io temp.display.item set from storage math:class items[2]
tellraw @a {"score":{"name":"loop","objective":"int"}}
#省略...
tellraw @a {"score":{"name":"loop","objective":"int"}}
#循环迭代
scoreboard players remove loop int 1
execute if score loop int matches 1.. run function math:graph/randnew/loop
```

输出是1,0,0

```mcfunction
#math:graph/randnew/loop
data modify storage math:io temp.to set value []
tellraw @a {"score":{"name":"loop","objective":"int"}}
#省略...
execute if score sloop int matches 1.. run function math:graph/randnew/sloop
tellraw @a {"score":{"name":"loop","objective":"int"}}
#省略...
execute if score random int matches 2 run data modify storage math:io temp.display.item set from storage math:class items[2]
tellraw @a {"score":{"name":"loop","objective":"int"}}
#省略...
```

输出是1,0,0

```mcfunction
#math:graph/randnew/loop
data modify storage math:io temp.to set value []
tellraw @a {"score":{"name":"loop","objective":"int"}}
#省略...
data modify storage math:io temp.name set from block 0 11 0 Text1
tellraw @a {"score":{"name":"loop","objective":"int"}}
#省略...
execute if score sloop int matches 1.. run function math:graph/randnew/sloop
tellraw @a {"score":{"name":"loop","objective":"int"}}
#省略...
```

输出是1,0,0

```mcfunction
#math:graph/randnew/loop
data modify storage math:io temp.to set value []
tellraw @a {"score":{"name":"loop","objective":"int"}}
#名字
function math:randstring/_pull
tellraw @a {"score":{"name":"loop","objective":"int"}}
data modify block 0 11 0 Text1 set value '{"nbt":"result","storage":"math:io","interpret":true}'
data modify storage math:io temp.name set from block 0 11 0 Text1
tellraw @a {"score":{"name":"loop","objective":"int"}}
#省略...
```

输出是1,0,0

于是我们最终把问题锁定到了math:randstring/_pull函数。经过排查，我们发现，这是一个嵌套执行的兼容性没有维护好导致的bug：循环变量<loop,int>在math:randstring/_pull函数中也被使用了。使用s命名法把它修改为sloop即可。