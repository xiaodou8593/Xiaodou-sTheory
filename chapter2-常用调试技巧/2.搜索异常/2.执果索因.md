# 执果索因

还记得黑箱测试要求测试函数的可重复性吗？当我们搜索bug时，执果索因法便建立在这基础之上。

当测试到的非预期现象可以迅速定位到问题命令时（例如实体没移动，可以确定是tp命令未执行；物品没修改，可以确定是item命令未执行），执果索因法便非常有效。

执果索因法的基本思路是这样的：

1.我们从显而易见的“效果命令”出发，检查这个效果命令为什么没有运行。

2.如果这个效果命令是无条件运行的，显然是因为这条命令所在的函数未被运行（不考虑超过命令执行上限的情况）。此时考虑这个函数是否被游戏加载，扩展名打错和语法错误都可能导致未加载。如果已加载，那么我们把这条命令所在的函数作为新的“效果命令”，从头重复进行执果索因。

3.如果这个效果命令是有条件运行的，可能是由于这条命令所在的mcfunction未被运行，也可能是这条命令的条件未通过。此时我们需要在这个mcfunction的开头加上一句无条件的`say 1`，重复运行测试函数（这体现了可重复性的重要性），检查是否输出1。如果没有输出1，该mcfunction确实没有运行，同步骤2。如果输出1，该mcfunction确实运行了，那么问题便是条件未通过。

4.对于许多的条件，我们可以在效果命令之前（注意是紧挨着的之前，这可以保证条件未经过改变）插入`execute if condition run say 1`，重复运行测试函数，检查是否输出1。对每个条件进行测试后，可以锁定是哪个条件中断了execute的执行（注意，这里的条件也可能是execute as、execute on、execute at这种可以中断的子命令）。

5.分析这个条件所判断的临时对象的来源，具体是哪个函数的输出。对输出该临时对象的函数重复进行测试和搜索，直到寻找到问题的源头。

执果索因的实战：

\#例1：我们以黑箱测试中发现问题的图可视化算法为例，用执果索因的方式搜索bug。以下为涉及到问题的全部指令：

```mcfunction
#math:graph/_display
#显示函数，对临时对象进行显示
summon area_effect_cloud 0 11 0 {Tags:["uuid_marker"]}
execute store result score loop int if data storage math:io list[]
execute if score loop int matches 1.. as @e[tag=uuid_marker,limit=1] run function math:graph/display/loop
kill @e[tag=uuid_marker]

#math:graph/display/loop
execute unless data storage math:io list[0].uuid run function math:graph/display/init
data modify entity @s Owner set from storage math:io list[0].uuid
execute on owner run function math:graph/display/set
#循环迭代
data modify storage math:io list append from storage math:io list[0]
data remove storage math:io list[0]
scoreboard players remove loop int 1
execute if score loop int matches 1.. run function math:graph/display/loop

#math:graph/display/set
#显示实体
execute on passengers run function math:graph/display/set_psg
#位置同步
execute store result score u int run data get storage math:io list[0].display.uvw[0] 10000
execute store result score v int run data get storage math:io list[0].display.uvw[1] 10000
execute store result score w int run data get storage math:io list[0].display.uvw[2] 10000
function math:uvw/_topos
```

在这里，math:uvw/_topos的作用是将临时对象{<u,int>,<v,int>,<w,int>}表示的局部坐标转换为世界坐标，并输出到执行者的Pos。

在黑箱测试中，我们发现问题所在：节点实体在召唤位置，并未被安排到预期的显示位置。而math:uvw/_topos便是把节点实体安排到显示位置。这里假设math:uvw/_topos是已经编写好的外部工具，不考虑它出现bug。

因此，我们首先推测，math:graph/display/set并未被执行，导致安排显示位置的math:uvw/_topos没有被执行。

于是我们检查math:graph/display/set是否已被执行，在第一行写上`say 1`即可。

```mcfunction
#math:graph/display/set
say 1
#显示实体
execute on passengers run function math:graph/display/set_psg
#位置同步
execute store result score u int run data get storage math:io list[0].display.uvw[0] 10000
execute store result score v int run data get storage math:io list[0].display.uvw[1] 10000
execute store result score w int run data get storage math:io list[0].display.uvw[2] 10000
function math:uvw/_topos
```

重新运行测试函数math:test后发现，果然没有输出1。

于是我们继续向上溯因，math:graph/display/set为什么没被执行？我们找到它的调用指令：`execute on owner run function math:graph/display/set`

可见on owner检索失败了。为什么执行者不存在owner？我们尝试输出执行者的Owner标签：

```mcfunction 
#math:graph/display/loop
execute unless data storage math:io list[0].uuid run function math:graph/display/init
data modify entity @s Owner set from storage math:io list[0].uuid
tellraw @a {"nbt":"Owner","entity":"@s"}
execute on owner run function math:graph/display/set
#循环迭代
data modify storage math:io list append from storage math:io list[0]
data remove storage math:io list[0]
scoreboard players remove loop int 1
execute if score loop int matches 1.. run function math:graph/display/loop
```

可以发现，Owner其实是存在的。于是我们思考是不是on owner本身的特性导致失败。通过查询指令相关文档我们发现，原来execute on owner仅用于可驯服生物，不能用于药水云。因此我们可以换成使用物品掉落物的Thrower和execute on origin实现相同的效果。